--PacemakerAAI.vdmsl

\section{AAI mode requirements}

The AAI code states

\begin{itemize}
\item[A] Pace the Atria.
\item[A] Sensing the Atria chamber.
\item[I] With inhibited response to sensing. A sense in the Atria inhibits a scheduled pace in it. 
\end{itemize}

\noindent This means the pacemaker must pace the atria chamber regarding the valid sensed data from the atria and the programmable parameters.


\paragraph{Programmable parameter requirements}

\begin{itemize}
\item LRL\ref{sym:LRL} shall be 60 ppm
\item URL\ref{sym:URL} shall be 120 ppm
\item ARP\ref{sym:ARP} shall be 250 ms
\item PVARP\ref{sym:PVARP} shall be 250 ms
\end{itemize}

\subsection*{Purpose and abstraction level}

The purpose is to model and validate the requirements of the Pacemaker AAI mode.

\begin{itemize}
\item Atrial Amplitude, Atrial Pulse With and Sensivity are discarded because they are not relevant for the propose.
\item In this mode the chamber is always the atria so the other chambers are discarded in the model.
\end{itemize}

\subsection*{VDM-SL module}

 The model of the requirements start by defining the input of the system as a sequence of senses in a chamber at a given time.

 The model of the requirements start by defining the value of the LRL.

\begin{vdm_al}
module PacemakerAAI

definitions 

values 
LRL     : nat = 60;
ARP     : nat = 250;
\end{vdm_al}

The input is a sequence and of sensed stimuli and the time of it.

\begin{vdm_al}
types 
SenseTimeline = seq of Sense;

Sense = <NONE> | <PULSE>;

\end{vdm_al}

The output will be a sequence of the reactions to the input that can be either do nothing or discharge a pulse on the Atria at a particular time.

\begin{vdm_al}
ReactionTimeline = seq of Reaction; 

Reaction = <NONE> | <PULSE>;
   
functions
\end{vdm_al}

The pacemaker its modelled as an implicit function stating that the bpm rate is larger or equal the LRL\cite{www:PacemakerCycles} and that every valid stimuli from the input triggers an artificial pulse response. The invalid ones are filtered by the ARP interval.

\begin{vdm_al}                         
Pacemaker (inp : SenseTimeline) r : ReactionTimeline
post let m = {i | i in set inds r & r(i) = <PULSE>}
     in len r = len inp 
        and
        r(1) = <PULSE> 
        and
        forall x in set m & (
           (exists y in set m & y > x) => 
                 (exists z in set m &  z >= x and z - x <= 60000/LRL)
                 or
                 (exists z in set inds inp &  z >= x and z - x > ARP and inp(z) = <PULSE>)
           );


end PacemakerAAI
\end{vdm_al}

\subsection{Requirements review}

\begin{itemize}
\item LRL \textbf{Modeled}
\item URL \textbf{Not modelled}\footnote{We discovered this requirement is contradictory and its a minor error in a table from the specification.}
\item ARP \textbf{Modeled}.
\item PVARP available to modes with atrial sensing is the time interval following a ventricular event when an atrial cardiac event shall not 
   \begin{enumerate}
   \item Inhibit an atrial pace. 
   \item Trigger a ventricular pace.
   \end{enumerate} \textbf{Not modelled}\footnote{We discovered this requirement is contradictory and its a minor error in a table from the specification.}
\end{itemize}


%\begin{rtinfo}
%[TotalxCoverage]{vdm.tc}[PacemakerAAI]
%Pace
%Pulse
%\end{rtinfo}

%>> p PacemakerAAI`post_Pacemaker({1|-><NONE>,250 |-> <PULSE>},{ 1 |-> <PULSE>, 500|-> <PULSE>})

%Should return false



--PacemakerAAT.vdmsl

\section{AAT mode requirements}

The AAT code states

\begin{itemize}
\item[A] Pace the Atria.
\item[A] Sensing the Atria chamber.
\item[T] With triggered response to sensing. A sense in the Atria triggers an immediate pace in it. 
\end{itemize}

\noindent meaning the pacemaker must pace the atria chamber regarding the valid sensed data from the atria and the programmable parameters.


\paragraph{Programmable parameter requirements}

\begin{itemize}
\item LRL\ref{sym:LRL} shall be 60 ppm
\item URL\ref{sym:URL} shall be 120 ppm
\item ARP\ref{sym:ARP} shall be 250 ms
\item PVARP\ref{sym:PVARP} shall be 250 ms
\end{itemize}

\paragraph{Purpose and abstraction level}

The purpose is to model and validate the requirements of the Pacemaker AAT mode.

\begin{itemize}
\item Atrial Amplitude, Atrial Pulse With and Sensivity are discarded because they are not relevant for the propose.
\item In this mode the chamber is always the atria so the other chambers are discarded in the model.
\end{itemize}

\subsection*{VDM-SL module}

 The model of the requirements start by defining the input of the system as a sequence of senses in a chamber at a given time.

 The model of the requirements start by defining the value of the LRL.


\begin{vdm_al}
module PacemakerAAT

definitions 

values 
LRL     : nat = 60;
ARP     : nat = 250;
\end{vdm_al}

The input is a sequence and of sensed stimuli and the time of it.

\begin{vdm_al}
types 
SenseTimeline = seq of Sense ;

Sense = <NONE> | <PULSE>;

Time = nat1;
\end{vdm_al}

The output will be a sequence of the reactions to the input that can be either do nothing or discharge a pulse on the Atria at a particular time.

\begin{vdm_al}
ReactionTimeline = seq of Reaction;

Reaction = <NONE> | <PULSE>;
   
functions
\end{vdm_al}

The pacemaker its modelled as an implicit function stating that the bpm rate is larger or equal the LRL\cite{www:PacemakerCycles} and  all the valid stimuli from the input triggers an artificial pulse response. The invalid ones are filtered by the ARP interval.

\begin{vdm_al}                         
Pacemaker (inp : SenseTimeline) r : ReactionTimeline
post let m = {i | i in set inds r & r(i) = <PULSE>}
     in len r = len inp 
        and
        forall x in set m & (
           (exists y in set m & y > x) => 
                 (exists z in set m &  z >= x and z - x <= 60000/LRL)
           );


end PacemakerAAT
\end{vdm_al}

\paragraph{Requirements review}

\begin{itemize}
\item LRL \textbf{Modeled}
\item URL \textbf{Not modelled}\footnote{We discovered this requirement is contradictory and its a minor error in a table from the specification.}
\item ARP \textbf{Modeled}.
\item PVARP available to modes with atrial sensing is the time interval following a ventricular event when an atrial cardiac event shall not 
   \begin{enumerate}
   \item Inhibit an atrial pace. 
   \item Trigger a ventricular pace.
   \end{enumerate} \textbf{Not modelled}\footnote{We discovered this requirement is contradictory and its a minor error in a table from the specification.}
\end{itemize}


%
%Pace
%Pulse
%\end{rtinfo}



--PacemakerAOO.vdmsl

\section{AOO mode requirements}

The AOO code states

\begin{itemize}
\item[A] Pace the Atria
\item[O] Without sensing the chambers
\item[O] Without response to sensing
\end{itemize}

\noindent meaning that the pacemaker must pace the atria chamber discarding any sensed data from the chambers just regarding the programmable parameters.

\paragraph{Programmable parameters requirements}

\begin{itemize}
\item LRL\ref{sym:LRL} shall be 60 ppm
\item URL\ref{sym:URL} shall be 120 ppm
\end{itemize}

\paragraph{Purpose and abstraction level}

The purpose is to model and validate the requirements of the Pacemaker AOO mode.

\begin{itemize}
\item Atrial Amplitude, Atrial Pulse With and Sensitivity are discarded because they are not relevant for the propose and don't add any understanding of the mode of pacing.
\item In this mode the paced/sensed chamber is always the atria so the other chambers are discarded in the model.
\end{itemize}

\subsection*{VDM-SL module}

 The model of the requirements start by defining the value of the LRL.

\begin{vdm_al}
module PacemakerAOO

definitions 

values 
LRL     : nat = 60;
\end{vdm_al}

The input is a sequence and each element of it corresponds to a time unit abstraction (in this case 1 millisecond).

\begin{vdm_al}
types 
SenseTimeline = map Time to Sense;

Sense = <NONE> | <PULSE>;

Time = nat1;
\end{vdm_al}

The output will be a sequence of the reactions to the input that can be either do nothing or discharge a pulse on the Atria.

\begin{vdm_al}
ReactionTimeline = map Time to Reaction; 

Reaction = <NONE> | <PULSE>;
   
functions
\end{vdm_al}

\subsubsection*{From LRL to ppm}

Considering that the ppm rate is given by the following formula
 
\begin{displaymath}
ppm = \frac{numberOfPulses}{timeInMinutes}
\end{displaymath}

\noindent and converting it to the modelled time unit: (milliseconds)

\begin{displaymath}
ppm = \frac{60000}{ms}
\end{displaymath}
We get
\begin{displaymath}
ms = \frac{60000}{ppm} 
\end{displaymath}

The period of the LRL is

\begin{displaymath}
LRLperiod_{observed} = \frac{numberOfPulses}{numberOfElementsObserved} 
\end{displaymath}

To compare the observed value to the LRL expressed in ppm, we need to convert it to milliseconds:

\begin{displaymath}
LRLperiod_{expected} = \frac{60000}{LRL}
\end{displaymath}

And because LRL is defined as a minimum, the pacemaker it is modelled as an implicit function stating that the ppm rate is larger or equal the LRL\cite{www:PacemakerCycles}.

\begin{displaymath}
LRLperiod_{observed} <= LRLperiod_{expected}
\end{displaymath}



\begin{vdm_al}                         
Pacemaker (inp : SenseTimeline) r : ReactionTimeline
post let m = {i | i in set dom r & r(i) = <PULSE>}
     in card dom r = card dom inp 
        and
        card dom inp > 1 => r(1) = <PULSE> 
        and
        forall x in set m & (
           (exists y in set m & y > x) => 
                 (exists y in set m & abs(x - y) <= 60000/LRL and x <> y));

end PacemakerAOO
\end{vdm_al}

\paragraph{Requirements review}

\begin{itemize}
\item LRL \textbf{Modelled}
\item URL is the maximum rate at which the paced ventricular rate will track sensed atrial events. The URL interval is the minimum time between a ventricular event and the next ventricular pace.\textbf{Not modelled}\footnote{We discovered this requirement is contradictory and its a minor error in a table from the specification.}
\end{itemize}




--PacemakerAOOR.vdmsl

\section{Reaching AOOR mode}

The AOOR mode is identical to the AOO mode except that the LRL value is adjusted
dynamicaly using the values of the Pacemaker accelerometer

\subsection{Requirements}

In this mode the following programmable parameters must be taken into account
while pacing:

\begin{itemize}
\item The device shall have the ability to adjust the cardiac cycle in response to metabolic need as measured from body motion using an accelerometer. 
%Page 32
\item LRL number of generator pulses delivered per minute.
\item LRLs LRL interval start. Shall start at a sensed or paced event.
\item LRLf The end of the LRL interval.
\item MSR  Maximum pacing rate allowed as a resolt of sensor control.
\item ActivityThreshold The value the accelerometer sensor output shall exceed  before the pacemaker's rate is afected by activity data.
\item ResponseFactor \ldots
\item ReactionTime is the time required to drive the rate from LRL to MSR. 
\item RecoveryTime is the time required to drive the rate from MSR to LRL.
\end{itemize}

The accelerometer shall determine the rate of increse of the pacing rate. (page 33 s 5.7.4)
The accelerometer shall determine the rate decrease of the pacing rate. (page 33 s 5.7.5)


\subsection{VDM-SL module}

In this operational mode the system will accept as input a sequence of
triples where

\begin{itemize}
\item LeadSense models the absence or not of a pulse.
\item Chamber the heart cavity from which the sence comes.
\item ActivityOutput the values outputed from the Accelerometer.
\end{itemize}

Each element in the sequence represents 1/4 of second.

Because this is a rate-adaptive mode we must take into account the values comming from the accelerometer. We will only model three of this values oposing to the seven in the requirements because theres an well defined abstraction between this two linear orders.

\begin{itemize}
\item{LOW} 0
\item{MED} 1
\item{HIGH} 2
\end{itemize}  


\begin{vdm_al}
module PacemakerAOOR

definitions 

types 

Time = nat;

SenseTimeline = seq of (Sense * [AccelerometerData] * Time)
inv stl == let l = [stl(i).#2 | i in set inds stl & stl(i).#2 <> nil]
           in l(1) = HIGH and forall i in set inds l & l(i) < MED => l(i-1) > MED;

AccelerometerData = nat
inv n == n < 3;

Sense = <NONE> | <PULSE>;

\end{vdm_al}

The reaction of our system will be a sequence of Lead paces or absence of it in each chamber.
 
\begin{vdm_al}

ReactionTimeline = seq of (Reaction * Time); 

Reaction = <NONE> | <PULSE>;
\end{vdm_al}

\begin{vdm_al}
state Sigma of
   LRL                : nat
   LRLs               : nat
   LRLf               : nat
   MSR                : nat
   ActivityThreshold  : AccelerometerData
   ReactionTime       : nat
   RecoveryTime       : nat
   rateChangePlan     : map nat to (<INC> | <DEC>)
init s == s = mk_Sigma(60,0,2,120,MED,10,2,{|->})
end   
   

operations

Pacemaker : SenseTimeline ==> ReactionTimeline
Pacemaker(inp) == 
   return if inp = [] 
          then []
          else [HeartController(hd inp)] ^ Pacemaker(tl inp);
                         
HeartController : (Sense * [AccelerometerData] * Time) ==> (Reaction * Time)
HeartController (mk_(-,acc,time)) == 
  (
   if acc <> nil then AdjustRate(acc,time);        
   if time in set dom rateChangePlan then applyChange(rateChangePlan(time));
   if LRLf <= LRLs 
   then (
          LRLs := 1; 
          return mk_(<PULSE>,time)
        )
   else (
          LRLs := LRLs + 1; 
          return mk_(<NONE>,time)
        ); 

   );   

 applyChange : <INC> | <DEC> ==> ()
 applyChange (a) == if a = <INC> then LRLf := 1
                                 else LRLf := 2;
 
 AdjustRate : AccelerometerData * Time ==> ()
 AdjustRate(act,time) == 
    if act > ActivityThreshold
    then rateChangePlan := {time + 10*2 |-> <INC>}
    else rateChangePlan := {time + 120*2 |-> <DEC>}


 

                             
values 

MED  : AccelerometerData = 1;
LOW  : AccelerometerData = 0;
MED  : AccelerometerData = 1;
HIGH : AccelerometerData = 2;

sensedData : seq of (Sense * [AccelerometerData] * Time) = 
[mk_(<NONE>,nil,i) | i in set {1,...,120}]^
[mk_(<NONE>,HIGH,121)]^
[mk_(<NONE>,nil,i) | i in set {121,...,190}]^
[mk_(<NONE>,LOW,191)]^
[mk_(<NONE>,nil,i) | i in set {192,...,436}];	

end PacemakerAOOR
\end{vdm_al}


--PacemakerDDD.vdmsl

\begin{vdm_al}
module PacemakerDDD

definitions 

values 
LRL     : nat = 60;   -- ppm
ARP     : nat = 250;  -- ms
VRP     : nat = 320;  -- ms
PVARP   : nat = 250;  -- ms
AVD     : nat = 150;  -- ms
VAD     : nat = 850; -- ms

types 
SenseTimeline = set of (Time * Chamber);

Chamber = <ATRIUM> | <VENTRICLE>;

Time = int;

Alarm = nat;

ReactionTimeline = set of (Time * Chamber); 
   

functions

Pacemaker : Time * SenseTimeline -> ReactionTimeline
Pacemaker (t,s) == PM(mk_(1,t,s,{},1000,0,-ARP,-VRP)).#1;


PM : (Time * Time * SenseTimeline * ReactionTimeline * Alarm * Alarm * Time * Time) -> 
                                   ReactionTimeline * Alarm * Alarm * Time * Time
PM (mk_(i,t,s,r,AA,VA,LastA,LastV)) == 

                                 if i = t
                                 then mk_(r,AA,VA,LastA,LastV)
                                 else if mk_(i,<ATRIUM>) in set s 
                                      then PM(c(i+1,t,s,SensedAtrium(i,r,AA,VA,LastA,LastV)))
                                      elseif mk_(i,<VENTRICLE>) in set s 
                                          then PM(c(i+1,t,s,SensedVentricle(i,r,AA,VA,LastA,LastV)))
                                          else PM(c(i+1,t,s,SensedNothing(i,r,AA,VA,LastA,LastV)));


SensedAtrium : Time * ReactionTimeline * Alarm * Alarm * Time * Time -> ReactionTimeline * Alarm * Alarm * Time * Time
SensedAtrium (t,r,AA,VA,LastA,LastV) == 
 
                             if t - LastA < ARP or VA > 0 or t - LastA < PVARP   -- 5.4.2  or 5.4.5 or 5.4.3
                             then SensedNothing(t,r,AA,VA,LastA,LastV)
                             else mk_(r,0,t + AVD,t,LastV); -- valid sense + schedule Ventricle


SensedVentricle : Time * ReactionTimeline * Alarm * Alarm * Time * Time -> ReactionTimeline * Alarm * Alarm * Time * Time
SensedVentricle (t,r,AA,VA,LastA,LastV) == 

                                if t - LastV < VRP -- 5.4.3
                                then SensedNothing(t,r,AA,VA,LastA,LastV)
                                else mk_(r,t + VAD,0,LastA,t); -- valid sense + unset ventricle alarm


SensedNothing : Time * ReactionTimeline * Alarm * Alarm * Time * Time -> ReactionTimeline * Alarm * Alarm * Time * Time
SensedNothing (t, r, AA, VA,LastA,LastV) == 
             
             if AA > 0 and t >= AA                                           -- Atrium alarm is set and fired
             then mk_(r union {mk_(t,<ATRIUM>)}, 0, t + AVD,t,LastV)   -- atrial pulse + schedule ventrile
             elseif VA > 0 and t >= VA                                       -- Ventricle alarm is set and fired
                 then mk_(r union {mk_(t,<VENTRICLE>)}, t + VAD, 0,LastA,t)       -- pulse ventricle + unset timer
                 else mk_(r, AA, VA,LastA,LastV);                            -- no alarms




-- Auxiliar funtcions

-- A curry function
c : Time * Time * SenseTimeline * (ReactionTimeline * Alarm * Alarm * Time * Time) -> 
                    Time * Time * SenseTimeline * ReactionTimeline * Alarm * Alarm * Time * Time
c (i,t,s,mk_(r,a,v,la,lv)) == mk_(i,t,s,r,a,v,la,lv);

end PacemakerDDD
\end{vdm_al}


--PacemakerDOO.vdmsl

\section{DOO mode requirements}

The DOO code states

\begin{itemize}
\item[D] Pace the atria and ventricle
\item[O] Without sensing the chambers
\item[O] Without response to sensing
\end{itemize}

\noindent This means the pacemaker must pace the atria chamber and ventricle discarding any sensed data from the chambers just regarding the programmable parameters.

\paragraph{Programmable parameter requirements}

\begin{itemize}
\item LRL\ref{sym:LRL} shall be 60 ppm.
\item URL\ref{sym:URL} shall be 120 ppm.
\item Fixed AV\ref{sym:FixedAVDelay} shall be 150 ms.
\end{itemize}

\paragraph{Purpose and abstraction level}

Model and validate the requirements of the Pacemaker DOO mode.

\begin{itemize}
\item Atrial/Ventricular Amplitude, Pulse Width and Sensitivity are discarded because they are not relevant for us, as they don't add any understanding of the mode of pacing.
\end{itemize}



\subsection*{VDM-SL module}

 The model of the requirements start by defining the input of the system as a set of the senses in a chamber at a given time.



\begin{vdm_al}
module PacemakerDOO

definitions 

types 

Time = nat;

SensedTimeline = set of (Chamber * Time);

Chamber = <ATRIA> | <VENTRICLE>;
\end{vdm_al}

Reactions will be an identical set but representing the discharged pulses.

\begin{vdm_al}
ReactionTimeline = set of (Chamber * Time);

\end{vdm_al}

The programmable parameters are defined as values.

\begin{vdm_al}
values
   LRL     : nat = 60;
   URL     : nat = 120;
   FixedAV : nat = 150;
\end{vdm_al}

The Pacemaker system transforms the input set into an output set containing the amount of atrial and ventricular pulses in order to achieve the expected ppm rate. The FixedAV requirement is expressed in the last universal quantifier.

\begin{vdm_al}                         
functions

Pacemaker (mk_(inp,n) : SensedTimeline * nat1) r : ReactionTimeline
post let nPulsesAtria = card {i | i in set r & i.#1 = <ATRIA>}, 
         nPulsesVentricle = card {i | i in set r & i.#1 = <VENTRICLE>}
     in  nPulsesAtria / n >= (LRL / 60) / 1000
         and
         nPulsesVentricle / n <= (URL / 60) / 1000
         and
         forall mk_(<ATRIA>,ta) in set r & (exists mk_(<VENTRICLE>,tv) in set r & tv = ta + FixedAV) ;
	                             
end PacemakerDOO
\end{vdm_al}

\paragraph{Requirements review}

\begin{itemize}
\item LRL \textbf{Modeled}.
\item URL \textbf{Modeled}.
\item Fixed AV \textbf{Modeled}.
\end{itemize}


--RateController.vdmsl

\section{XXXR modes requirements}

The modes ending in R like AOOR require that we adjust the rate for that we had to model the component that will adjust the rate.

\paragraph{Programmable parameters requirements}

In this mode the following programmable parameters must be taken into account
while pacing:

\begin{itemize}
\item LRL shall be 60 ppm.
\item MSR shall be 120 ppm.
\item ActivityThreshold shall be \textbf{Med}.
\item ResponseFactor shall be 8.
\item ReactionTime shall be 30 s. 
\item RecoveryTime shall be 5 m.
\end{itemize}



\paragraph{Purpose and abstraction level}

The rate controller full functionality is not modelled, we represent all the variables needed to control the increase and decrease in rate. But the changes will be instantaneous disregarding the response and recovery time delays. These are requirements for the rate change and not for the operating mode. 

The purpose it to understand the different operation modes not how the rate changes, so its logical that if the rate can change on an operation mode for instance AOOR we model that change but in an abstract way.

\subsection*{VDM-SL module}

The rate controller will accept as input a sequence of Accelerometer inputs:

\begin{vdm_al}
module RateController

definitions 

types 

Input = map Time to ActivityData;
\end{vdm_al}

Activity data is mapped to a subset of nat1 as:

\begin{itemize}
\item{V-LOW} 1
\item{LOW} 2
\item{MED-LOW} 3
\item{MED} 4
\item{MED-HIGH} 5
\item{HIGH} 6
\item{V-HIGH} 7
\end{itemize}  

Time is abstracted as a nat.

\begin{vdm_al}
Time = nat1;

ActivityData = nat1
inv a == a <= 7;

\end{vdm_al}

The response factor is an integer number betwen 1 and 16.

\begin{vdm_al}
RF = nat1
inv rf == rf <= 16;
\end{vdm_al}

The reaction of our system will be a comand to change the rate, in this case 
we model the ouput as 
 
\begin{vdm_al}

Output = map Time to PPM; 

PPM = nat1
inv ppm == ppm >= 30 and ppm <= 175;

\end{vdm_al}

The programmable parameters are declared as values.

\begin{vdm_al}
values
   LRL           : PPM = 60;
   MSR           : PPM = 120;
   Threshold     : ActivityData = 6;
   ReactionTime  : Time = 150;
   ResponseFactor: RF = 8; -- Not understood
   RecoveryTime  : Time = 5;
\end{vdm_al}   

Finally the simulation of the rate controller follows as a relation between the reach of the MSR with a exceeding input value of the treshold, and the LRL as a decrease after the reacovery time form the MSR or the normal functioning of the system.

\begin{vdm_al}
functions

Simulate(inp : Input)  out : Output
pre 0 not in set dom inp
post forall t in set dom inp &
            (out(t) = MSR =>  inp(t-ReactionTime) > Threshold or out(t-1) = MSR)
     and
     forall t in set dom inp \ {1} & 
            (out(t) = LRL =>  inp(t-RecoveryTime) < Threshold or out(t-1) = LRL);




end RateController
\end{vdm_al}

\subsection{Requirements review}

\begin{itemize}
\item LRL \textbf{Modelled}.
\item MSR \textbf{Modelled}.
\item ActivityThreshold \textbf{Modelled}.
\item ResponseFactor \textbf{Not understood}.
\item ReactionTime \textbf{Modelled}.
\item RecoveryTime \textbf{Modelled}.
\end{itemize}