Packages
base org.overture.codegen.cgast;
analysis org.overture.codegen.cgast.analysis;
  
Tokens
/* This section declared tokens as internal types or external classes
 * internal definitions:
 *  plus='+';
 *  
 * external
 *  location = 'java:org.overture.ast.lex.LexLocation';
 */
 
  vdmType = 'java:org.overture.ast.types.PType';
  SourceNode = 'java:org.overture.codegen.ir.SourceNode';
  
  // Java
  java_Boolean = 'java:java.lang.Boolean';
  java_Long = 'java:java.lang.Long';
  java_Integer = 'java:java.lang.Integer';
  java_Double = 'java:java.lang.Double';
  java_Character = 'java:java.lang.Character';
  java_String = 'java:java.lang.String';
  java_Object = 'java:java.lang.Object';
  
  // Wraps a string type so it can be used as a field type
  eString ='java:org.overture.ast.util.ClonableString';
     
Abstract Syntax Tree
/* This section declares the AST by giving root nodes and their possible sub classe */

CG {-> package='org.overture.codegen.cgast'
	| [sourceNode]:SourceNode
	| (tag):java_Object // Tags carry information to support IR analysis
	| (metaData):eString*} // Meta data is intended to hold printable information such as code annotations
	= #pattern
	| #bind
	| #multipleBind
	| #name
	| #decl
	| #imports
	| #import
	| #exports
	| #export
	| #stateDesignator
	| #objectDesignator
	| #localParam
	| #stm
	| #letBeSt
	| #exp
	| #type
	| #modifier
	| #term
	| #traceDecl
	| #traceCoreDecl
	;

#imports {-> package='org.overture.codegen.cgast.declarations'} 
    = {module} [name]:java_String [imports]:CG.#imports.fromModule*
    | {fromModule} [name]:java_String [signatures]:CG.#import**
    ;
    
#import {-> package='org.overture.codegen.cgast.declarations'
	|	[name]:java_String 
	|	[renamed]:java_String 
	|	[fromModuleName]:java_String/*(from):CG.#decl.module*/}
    =   {all}
    |   {type} [decl]:CG.#decl.type
    |   #value
    ;
    
#value {-> package='org.overture.codegen.cgast.declarations'
	|	(importType):CG.#type}
    =   {value}
    |   {function} [typeParams]:CG.#name.token* //Because [typeParams]:java_String* is not supported by the AstCreator
    |   {operation}
    ;

#exports {-> package='org.overture.codegen.cgast.declarations'}
    =   {module} [exports]:CG.#export**
    ;

#export {-> package='org.overture.codegen.cgast.declarations'
	|	[decl]:CG.#decl*}
    =   {all}
    |   {function} [nameList]:CG.#name.token* (exportType):CG.#type
    |   {operation} [nameList]:CG.#name.token* (exportType):CG.#type
    |   {type} [name]:java_String [struct]:java_Boolean
    |   {value} [nameList]:CG.#name.token* (exportType):CG.#type
    ;

#pattern {-> package='org.overture.codegen.cgast.patterns'}
	= {identifier} [name]:java_String
	| {ignore}
	| {bool} [value]:java_Boolean
	| {char} [value]:java_Character
	| {int}	[value]:java_Long
	| {null}
	| {quote} [value]:java_String
	| {real} [value]:java_Double
	| {string} [value]:java_String
	| {tuple} [patterns]:CG.#pattern*
	| {record} [typename]:java_String [patterns]:CG.#pattern* [type]:CG.#type
	;

#bind {-> package='org.overture.codegen.cgast.patterns'
	|	[pattern]:CG.#pattern}
    =   {set} [set]:CG.#exp //binds
    |   {type} [type]:CG.#type
    ;
		
#multipleBind {-> package='org.overture.codegen.cgast.patterns'
	|	[patterns]:CG.#pattern*}
    =   {set}  [set]:CG.#exp //binds
    |   {type} [type]:CG.#type
    ;
   
#name {-> package='org.overture.codegen.cgast.name'}
	=	{type}
		[name]:java_String
		[definingClass]:java_String
	|	{token}
		[name]:java_String
	;
    
#decl {-> package='org.overture.codegen.cgast.declarations'}
	=	{method} //rename to operation
		(preCond):CG.#decl
		(postCond):CG.#decl
		[access]:java_String //Consider introducing access node covering both "access" and "async"
		[async]:java_Boolean
		[static]:java_Boolean
		[abstract]:java_Boolean
		[methodType]:CG.#type.method
		[name]:java_String
		[formalParams]:CG.#localParam.formalParam*
		[isConstructor]:java_Boolean
		[templateTypes]:CG.#type.template*
		[implicit]:java_Boolean
		[body]:CG.#stm
	|	{func}
		(preCond):CG.#decl
		(postCond):CG.#decl
		[access]:java_String
		[abstract]:java_Boolean
		[methodType]:CG.#type.method
		[name]:java_String
		[formalParams]:CG.#localParam.formalParam*
		[templateTypes]:CG.#type.template*
		[implicit]:java_Boolean
		[body]:CG.#exp
	|	{field} 
    	[access]:java_String
    	[static]:java_Boolean
    	[volatile]:java_Boolean
    	[final]:java_Boolean
    	[type]:CG.#type
    	[name]:java_String
    	[initial]:CG.#exp
   	|	#Class
    |	{module}
    	[name]:java_String
    	[import]:CG.#imports.module
    	[exports]:CG.#exports.module
    	[decls]:CG.#decl*
    	[isFlat]:java_Boolean
    	[isDLModule]:java_Boolean
    |   {state}
    	[access]:java_String
    	[executable]:java_Boolean
    	[name]:java_String
    	[fields]:CG.#decl.field*
    	[invPattern]:CG.#pattern
    	[invExp]:CG.#exp
    	[invDecl]:CG.#decl.func
    	[initPattern]:CG.#pattern
    	[initExp]:CG.#exp
    	[initDecl]:CG.#decl.func
    |	{interface}
    	[templateTypes]:CG.#type.template*
    	[package]:java_String
    	[name]:java_String
    	[fields]:field*
    	[methodSignatures]:CG.#decl.method*
    |	{record}
    	//TODO: Should have [access]:java_String
    	[name]:java_String
    	[invariant]:CG.#decl
    	[fields]:field*
    	[methods]:CG.#decl.method*
    |	{thread}
    	[def]:CG.#decl
    	[stm]:CG.#stm
    	[type]:CG.#type
    |	{type}
    	[access]:java_String
    	[inv]:CG.#decl
    	[decl]:CG.#decl
    |	{catchClause}
    	[type]:CG.#type
    	[name]:java_String
    	[stm]:CG.#stm
    |	{persync}
    	[opname]:java_String
    	[guardname]:java_String
    	[pred]:CG.#exp
    |	{mutexSync}
    	[opnames]:CG.#name*
    |	{var}
    	[final]:java_Boolean
    	[type]:CG.#type
    	[pattern]:CG.#pattern
    	[exp]:CG.#exp
    |	{namedType}
    	[name]:CG.#name.type
    	[type]:CG.#type
    |	{namedTrace}
    	[pathname]:CG.#name.token*
    	[terms]:CG.#term.traceDecl*
	;
	
#Class {-> package='org.overture.codegen.cgast.declarations'
  | (globalMetaData):eString*
  | [package]:java_String
  | [access]:java_String
  | [abstract]:java_Boolean
  | [static]:java_Boolean 
  | [name]:java_String
  | [fields]:field*
  | [invariant]:CG.#decl
  | [methods]:CG.#decl.method*
  | [functions]:CG.#decl.func*
  | [traces]:CG.#decl.namedTrace*
  | [superName]:java_String
  | [thread]:CG.#decl.thread
  | [innerClasses]:CG.#decl.class*
  | [interfaces]:CG.#decl.interface*
  | [typeDecls]:CG.#decl.type*
  | [perSyncs]:CG.#decl.persync*
  | [mutexSyncs]:CG.#decl.mutexSync*}
   = {bus}
   | {cpu}
   | {system}
   | {default}
  ;
	
#stateDesignator {-> package='org.overture.codegen.cgast.statements'
	| [type]:CG.#type }
	= {identifier} [name]:java_String [explicit]:java_Boolean [className]:java_String [isLocal]:java_Boolean
	| {field} [object]:CG.#stateDesignator [field]:java_String
	| {mapSeq} [mapseq]:CG.#stateDesignator [exp]:CG.#exp
	;

#objectDesignator {-> package='org.overture.codegen.cgast.statements'}
	=   {apply} [object]:CG.#objectDesignator [args]:CG.#exp*
    |   {field} [object]:CG.#objectDesignator [fieldModule]:java_String [fieldName]:java_String
    |   {identifier} [exp]:CG.#exp //Normally a var (e.g. [exp]:CG.#exp.#Var) but a transformation may need to replace it with a cast expression
    |   {new} [exp]:CG.#exp.new
    |   {self}
    ;
    
#localParam {-> package='org.overture.codegen.cgast.declarations'}
           = {formalParam} [type]:CG.#type [pattern]:CG.#pattern
           ;

#stm {-> package='org.overture.codegen.cgast.statements'}
           =	{if} [ifExp]:CG.#exp [thenStm]:CG.#stm [elseIf]:CG.#stm.elseIf* [elseStm]:CG.#stm
           | 	{elseIf} [elseIf]:CG.#exp [thenStm]:CG.#stm
           | 	{skip}
           |	{return} [exp]:CG.#exp
           |	{abstractBody}
           |	{assignment} [target]:CG.#stateDesignator [exp]:CG.#exp
           |	{assignToExp} [target]:CG.#exp [exp]:CG.#exp
           |	{localPatternAssignment} [target]:CG.#pattern [exp]:CG.#exp (nextElementDecl):CG.#decl.var
           |	{block}	[scoped]:java_Boolean [localDefs]:CG.#decl.var* [statements]:CG.#stm*
           |	{callObject} [type]:CG.#type [designator]:CG.#objectDesignator /*[className]:java_String*/ [fieldName]:java_String 
           					 [args]:CG.#exp*
           |	{callObjectExp} [type]:CG.#type [obj]:CG.#exp /*[className]:java_String*/ [fieldName]:java_String 
           					 [args]:CG.#exp*
           |	#call
           |	{notImplemented}
           |	{forIndex} [var]:java_String [from]:CG.#exp [to]:CG.#exp [by]:CG.#exp [body]:CG.#stm
           |	{forAll} [pattern]:CG.#pattern [exp]:CG.#exp [body]:CG.#stm
           |	{while} [exp]:CG.#exp [body]:CG.#stm
           |	{letBeSt} [header]:CG.#letBeSt.header [statement]:CG.#stm
           |	{throw} [exp]:CG.#exp
           |	{forLoop} [init]:CG.#decl.var [cond]:CG.#exp [inc]:CG.#exp [body]:CG.#stm
           |	{increment} [var]:CG.#exp
           |	{decrement} [var]:CG.#exp
           |	{raiseError} [error]:CG.#exp.#RuntimeError
           |	{cases} [exp]:CG.#exp [cases]:CG.#stm.#altStm.case* [others]:CG.#stm
           |	#altStm
           |	{error}
           |	{continue}
           |	{break}
           |	{start} [exp]:CG.#exp [type]:CG.#type
           |	{startlist} [exp]:CG.#exp [type]:CG.#type
           |	{try} [stm]:CG.#stm [catchClauses]:CG.#decl.catchClause* [finally]:CG.#stm
           |	{periodic} [opname]:java_String [args]:CG.#exp*
         //|	Sporadic
           |	{stackDecl}
           		[type]:CG.#type
           		[name]:java_String
           		[args]:CG.#exp*
			|	{mapSeqUpdate} [col]:CG.#exp [index]:CG.#exp [value]:CG.#exp
			|	{invCheck} [subject]:CG.#exp
			|	{atomic} [statements]:CG.#stm*
			| {cycles} [cycles]:CG.#exp [stm]:CG.#stm
			| {duration} [duration]:CG.#exp [stm]:CG.#stm
			|	{meta}
           ;

#call {-> package='org.overture.codegen.cgast.statements'
	| 	[type]:CG.#type
	|	[isStatic]:java_Boolean
	|	[name]:java_String
	|	[args]:CG.#exp*}
    =  	{plain} [classType]:CG.#type
    |	{super} 
    ;

           
#altStm {-> package='org.overture.codegen.cgast.statements'}
    = {case} [pattern]:CG.#pattern [result]:CG.#stm [patternType]:CG.#type
    ;
           
#letBeSt {-> package='org.overture.codegen.cgast.utils'}
	= {header} [binding]:CG.#multipleBind [suchThat]:CG.#exp //The Overture AST uses a multiplebind 
	;
	
#exp {-> package='org.overture.codegen.cgast.expressions'
	| [type]:CG.#type}
    =	#Unary
    |	#Binary
    |	{field} [object]:CG.#exp [memberName]: java_String
    |	{apply} [root]:CG.#exp [args]:CG.#exp*
	|	{new} [name]:CG.#name.type [args]:CG.#exp*
	|	#Var
	|	{instanceof} [checkedType]:CG.#type [exp]:CG.#exp
	|	#Is
    |	#Literal
    |	{self}
    |	{null}
    |	{letDef} [localDefs]:CG.#decl.var* [exp]:CG.#exp
    |	{methodInstantiation} [func]:CG.#exp [actualTypes]:CG.#type*
    |	{tuple} [args]:CG.#exp* [tupleType]:CG.#type.tuple
    |	{fieldNumber} [field]:java_Long [tuple]:CG.#exp
    |	{tupleSize} [tuple]:CG.#exp
    |	{ternaryIf} [condition]:CG.#exp [trueValue]:CG.#exp [falseValue]:CG.#exp
    |   {maplet}  [left]:CG.#exp [right]:CG.#exp
    |	{letBeSt} [header]:CG.#letBeSt.header [value]:CG.#exp
    |	#Seq
    |	#Set
    |	#Map
    |	{mkBasic} [arg]:CG.#exp
    |	{recordMod} [rec]:CG.#exp [recType]:CG.#type.record [modifiers]:CG.#modifier.record*
    |	#Quantifier
    |	{preInc} [exp]:CG.#exp
    |	{preDec} [exp]:CG.#exp
    |	{postInc} [exp]:CG.#exp
    |	{postDec} [exp]:CG.#exp
    |	{deRef} [exp]:CG.#exp //For pointer dereference
    |	#RuntimeError
        //When generating code using the abstract syntax of this tree structure "external" 
    	//is used for refering to expressions of the target environment (e.g. that of Java) 
    	//that are not directly mapped to by the code generator.
    |	{external} [targetLangExp]:java_String
    |	{typeArg} [type]:CG.#type
    |	{lambda} [params]:CG.#localParam.formalParam* [exp]:CG.#exp
    |	{anonymousClass} [methods]:CG.#decl.method*
    |	{notImplemented}
    |	{undefined}
    |	{threadId}
    |	{tupleCompatibility} [tuple]:CG.#exp [types]:CG.#type*
    |	{cases} [exp]:CG.#exp [cases]:CG.#exp.#altExp.case* [others]:CG.#exp
    |	#altExp
    |	{subSeq} [seq]:CG.#exp [from]:CG.#exp [to]:CG.#exp
    |	{history}[histype]:java_String [opsname]:java_String [sentinelType]:CG.#type.class
    |	{time}
    |	{assignExp} [target]:CG.#exp [value]:CG.#exp
    |	{mapSeqGet} [col]:CG.#exp [index]:CG.#exp
    ;

#modifier {-> package='org.overture.codegen.cgast.expressions'}
    = {record} [name]:java_String [value]:CG.#exp
    ;

#Is {-> package='org.overture.codegen.cgast.expressions'
	| [exp]:CG.#exp}
    =	{bool}
    |	{nat}
    |	{nat1}
    |	{int}
    |	{rat}
    |	{real}
    |	{char}
    |	{token}
    |	{tuple} [checkedType]:CG.#type
    |	{general} [checkedType]:CG.#type
    ;   

#altExp {-> package='org.overture.codegen.cgast.expressions'}
    = {case} [pattern]:CG.#pattern [result]:CG.#exp [patternType]:CG.#type
    ;

#RuntimeError {-> package='org.overture.codegen.cgast.expressions'}
    =   {LetBeStNoBinding}
    |	{patternMatch} [message]:java_String
    |	{missingMember} [message]:java_String
    	//Raised at runtime when an attemp is made 
		//to access a non-existing member (e.g. operation)
	|	{preCond} [message]:java_String
    ;

#Var {-> package='org.overture.codegen.cgast.expressions'
	|	[isLambda]:java_Boolean
	|	[isLocal]:java_Boolean
	|	[name]:java_String}
    =   {identifier}
	|	{explicit} [classType]:CG.#type
	|	{super}
    ;
    
#Literal {-> package='org.overture.codegen.cgast.expressions'}
    =	{int} [value]:java_Long
    |	{real} [value]:java_Double
    |	{bool} [value]:java_Boolean
    |	{char} [value]:java_Character
    |	{quote} [value]:java_String
    |	{string} [value]:java_String [isNull]:java_Boolean
	;
	
#Quantifier {-> package='org.overture.codegen.cgast.expressions'
	| [bindList]:CG.#multipleBind*
	| [predicate]:CG.#exp}
	= {forAll}
	| {exists}
	| {exists1}
	;
    
#Binary {-> package='org.overture.codegen.cgast.expressions'
	| [left]:CG.#exp
	| [right]:CG.#exp}
    =   #Numeric
    |	#Bool
    //Special tree construct NOT used when constructing the OO AST from 
    //the VDM model. The addr comparison constructs can, however, be convenient 
    //when generating real code (e.g., Java) that is first described using 
    //the abstract syntax of the OO AST.
    |	{addrEquals}
    |	{addrNotEquals} 
    |	{equals}
    |	{notEquals}
    |	{seqConcat}
    |	{seqModification}
    |   {inSet}
    |	{setUnion}
    |	{setIntersect}
    |	{setDifference}
    |	{setSubset}
    |	{setProperSubset}
    |	{mapUnion}
    |	{mapOverride}
    |	{domainResTo} //Syntax: '<:'
    |	{domainResBy} //Syntax: '<-:'
    |	{rangeResTo} //Syntax: ':>'
    |	{rangeResBy} //Syntax: ':->'
    ;
 
#Numeric {-> package='org.overture.codegen.cgast.expressions'}
    =	{intDiv}
    |	{divide}
    |	{greaterEqual}
    |	{greater}
    |	{lessEqual}
    |	{less}
    |	{mod}   
    |	{plus} 
    |   {subtract}
    |	{rem}
    |	{times}
    |	{power}       
    ;   

#Bool {-> package='org.overture.codegen.cgast.expressions'}
    =	{or}
    |	{and}
    |	{xor}
	;
 
#Unary {-> package='org.overture.codegen.cgast.expressions'
	| [exp]:CG.#exp}
    =   {plus} 
    |   {minus}
    |	{cast}
    |	{isolation}
    |	{len}
    |	{card}
    |	{elems}
    |	{indices}
    |	{head}
    |	{tail}
    |	{reverse}
    |	{floor}    
    |	{abs}
    |	{not}
    |	{distConcat}
    |	{distUnion}
    |	{distIntersect}
    |	{powerSet}
    |	{mapDomain}
    |	{mapRange} 
    |	{distMerge}
    |	{mapInverse}
    |	{seqToString}
    |	{stringToSeq}
    ;
    
#Seq {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp*
	| {comp} [first]:CG.#exp [setBind]:CG.#bind.set [set]:CG.#exp [predicate]:CG.#exp
	;

#Set {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp*
	| {comp} [first]:CG.#exp [bindings]:CG.#multipleBind* [predicate]:CG.#exp//In the Overture AST bindings are of type multipleBind* as it also allows type binds.
	| {range} [first]:CG.#exp [last]:CG.#exp
	;

#Map {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp.maplet*
	| {comp} [first]:CG.#exp.maplet [bindings]:CG.#multipleBind* [predicate]:CG.#exp
	;

#type {-> package='org.overture.codegen.cgast.types'
	| [optional]:java_Boolean
	| [namedInvType]:CG.#decl.namedType}
    =   #basic
    |	{Object}
    |	{void}
    |	{class} [types]:CG.#type* [name]:java_String
    	//When generating code using the abstract syntax of this tree structure "external" 
    	//is used for refering to types of the target environment (e.g. that of Java) 
    	//that are not directly mapped to by the code generator.
    |	{external} [name]:java_String [info]:externalType.info
    |	{record} [name]:CG.#name.type
    |	{string}
    |	{template} [name]:java_String
    |	{tuple} [types]:CG.#type*
    |   {method} [params]:CG.#type* [result]:CG.#type [equivalent]:vdmType
    |	#map
    |	#set
    |	#seq
    |	#basicTypeWrappers
    |	{error}
    |	{interface} [types]:CG.#type* [name]:java_String
    |	{union} [types]:CG.#type*
    |	{quote} [value]:java_String
    |	{unknown}
    ;
    
externalType {-> package='org.overture.codegen.cgast.utils'}
	= {info} [namespace]:java_String [filePath]:java_String 
	;

#basicTypeWrappers {-> package='org.overture.codegen.cgast.types'}
	= {Int}
	| {Nat1}
	| {Nat}
	| {Rat}
	| {Real}
	| {Char}
	| {Bool}
	;

#set {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [setOf]:CG.#type}
    =   {set} 
    ;
    
#seq {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [seq1]:java_Boolean
	| [seqOf]:CG.#type}
    =   {seq}
    ;
    
#map {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [injective]:java_Boolean
	| [from]:CG.#type
	| [to]:CG.#type }
    =   {map}
    ;
    
#basic {-> package='org.overture.codegen.cgast.types'}
    =   #numeric
    |	{char}
    |	{bool}
    |	{token}
    ;

#numeric {-> package='org.overture.codegen.cgast.types'}
    =   {int}
    |	{nat1}
    |	{nat}
    |	{rat}
    |   {real} 
    ;

#term {-> package='org.overture.codegen.cgast.traces'}
    =   {traceDecl} [traceDecls]:CG.#traceDecl*
    ;
    
#traceDecl {-> package='org.overture.codegen.cgast.traces'}
    =   {instance} //Not there...
    |   {letBeStBinding}
    	[bind]:CG.#multipleBind.set //[bind]:CG.#multipleBind (Only set binds are supported)
    	[stExp]:CG.#exp
    	[body]:CG.#traceDecl
    |   {letDefBinding}
    	[localDefs]:CG.#decl.var*//[localDecls]:CG.#decl*
    	[body]:CG.traceDecl
    |   {repeat} [core]:CG.#traceCoreDecl [from]:java_Long [to]:java_Long
    ;
    
#traceCoreDecl {-> package='org.overture.codegen.cgast.traces'}
    =   {applyExp} [callStm]:CG.#stm
    |   {bracketedExp} [terms]:CG.#term.traceDecl*
    |   {concurrentExp} [decls]:CG.#traceDecl*
    ;
     
Aspect Declaration
/* This section allows fields of root types to be declared e.g. exp*/

//1) No check for clash wrt nameing of fields
//2) Only graphs
