Packages
base org.overture.codegen.cgast;
analysis org.overture.codegen.cgast.analysis;
  
Tokens
/* This section declared tokens as internal types or external classes
 * internal definitions:
 *  plus='+';
 *  
 * external
 *  location = 'java:org.overture.ast.lex.LexLocation';
 */
 
  vdmType = 'java:org.overture.ast.types.PType';
  SourceNode = 'java:org.overture.codegen.ir.SourceNode';

  
  //Java
  java_Boolean = 'java:java.lang.Boolean';
  java_Long = 'java:java.lang.Long';
  java_Integer = 'java:java.lang.Integer';
  java_Double = 'java:java.lang.Double';
  java_Character = 'java:java.lang.Character';
  java_String = 'java:java.lang.String';
  java_Object = 'java:java.lang.Object';
  
     
Abstract Syntax Tree
/* This section declares the AST by giving root nodes and their possible sub classe */

CG {-> package='org.overture.codegen.cgast'
	| [sourceNode]:SourceNode
	| (tag):java_Object}
	= #pattern
	| #bind
	| #multipleBind
	| #name
	| #decl
	| #stateDesignator
	| #objectDesignator
	| #localParam
	| #stm
	| #letBeSt
	| #exp
	| #type
	;

#pattern {-> package='org.overture.codegen.cgast.patterns'}
	= {identifier} [name]:java_String
	| {ignore}
	| {bool} [value]:java_Boolean
	| {char} [value]:java_Character
	| {int}	[value]:java_Long
	| {null}
	| {quote} [value]:java_String
	| {real} [value]:java_Double
	| {string} [value]:java_String
	| {tuple} [patterns]:CG.#pattern*
	| {record} [typename]:java_String [patterns]:CG.#pattern* [type]:CG.#type
	;

#bind {-> package='org.overture.codegen.cgast.patterns'
	|	[pattern]:CG.#pattern}
    =   {set} [set]:CG.#exp //binds
    ;
		
#multipleBind {-> package='org.overture.codegen.cgast.patterns'
	|	[patterns]:CG.#pattern*}
    =   {set}  [set]:CG.#exp //binds
    ;
   
#name {-> package='org.overture.codegen.cgast.name'}
	=	{type}
		[name]:java_String
		[definingClass]:java_String
	;
    
#decl {-> package='org.overture.codegen.cgast.declarations'}
	=	{method} //rename to operation
		[access]:java_String
		[static]:java_Boolean
		[abstract]:java_Boolean
		[methodType]:CG.#type.method
		[name]:java_String
		[formalParams]:CG.#localParam.formalParam*
		[isConstructor]:java_Boolean
		[templateTypes]:CG.#type.template*
		[body]:CG.#stm
	|	{func}
		[access]:java_String
		[abstract]:java_Boolean
		[methodType]:CG.#type.method
		[name]:java_String
		[formalParams]:CG.#localParam.formalParam*
		[templateTypes]:CG.#type.template*
		[body]:CG.#exp
	|	{field} 
    	[access]:java_String
    	[static]:java_Boolean
    	[final]:java_Boolean
    	[type]:CG.#type
    	[name]:java_String
    	[initial]:CG.#exp
    |	#Local
   	|	{class}
   		[package]:java_String
      	[access]:java_String
      	[abstract]:java_Boolean
      	[static]:java_Boolean 
    	[name]:java_String
    	[fields]:field*
    	[methods]:CG.#decl.method*
    	[functions]:CG.#decl.func*
    	[superName]:java_String
    	[records]:CG.#decl.record*
    	[thread]:CG.#decl.thread
    |	{interface}
    	[templateTypes]:CG.#type.template*
    	[package]:java_String
    	[name]:java_String
    	[fields]:field*
    	[methodSignatures]:CG.#decl.method*
    |	{record}
    	[name]:java_String
    	[access]:java_String
    	[fields]:field*
    	[methods]:CG.#decl.method*
    |	{empty}
    |	{thread}
    	[def]:CG.#decl
    	[stm]:CG.#stm
    	[type]:CG.#type
	;

#Local  {-> package='org.overture.codegen.cgast.declarations'}
    =	{var}
    	[type]:CG.#type
    	[pattern]:CG.#pattern
    	[exp]:CG.#exp
    |	{counter}
    	[name]:java_String
    	[init]:CG.#exp
	;
	
#stateDesignator {-> package='org.overture.codegen.cgast.statements'
	| [type]:CG.#type}
	= {identifier} [name]:java_String [explicit]:java_Boolean [className]:java_String
	| {field} [object]:CG.#stateDesignator [field]:java_String
	| {mapSeq} [mapseq]:CG.#stateDesignator [exp]:CG.#exp
	;

#objectDesignator {-> package='org.overture.codegen.cgast.statements'}
	=   {apply} [object]:CG.#objectDesignator [args]:CG.#exp*
    |   {field} [object]:CG.#objectDesignator [fieldModule]:java_String [fieldName]:java_String
    |   {identifier} [exp]:CG.#exp //Normally a var (e.g. [exp]:CG.#exp.#Var) but a transformation may need to replace it with a cast expression
    |   {new} [exp]:CG.#exp.new
    |   {self}
    ;
    
#localParam {-> package='org.overture.codegen.cgast.declarations'}
           = {formalParam} [type]:CG.#type [pattern]:CG.#pattern
           ;

#stm {-> package='org.overture.codegen.cgast.statements'}
           =	{if} [ifExp]:CG.#exp [thenStm]:CG.#stm [elseIf]:CG.#stm.elseIf* [elseStm]:CG.#stm
           | 	{elseIf} [elseIf]:CG.#exp [thenStm]:CG.#stm
           | 	{skip}
           |	{return} [exp]:CG.#exp
           |	{letDef} [localDefs]:CG.#decl.#Local.var* [stm]:CG.#stm
           |	{abstractBody}
           |	{assignment} [target]:CG.#stateDesignator [exp]:CG.#exp
           |	{localAssignment} [target]:CG.#exp [exp]:CG.#exp
           |	{localPatternAssignment} [target]:CG.#pattern [exp]:CG.#exp (nextElementDecl):CG.#decl.#Local.var
           |	{block}	[localDefs]:CG.#decl.#Local* [statements]:CG.#stm*
           |	{callObject} [type]:CG.#type [designator]:CG.#objectDesignator [className]:java_String [fieldName]:java_String 
           					 [args]:CG.#exp*
           |	{callObjectExp} [type]:CG.#type [obj]:CG.#exp [className]:java_String [fieldName]:java_String 
           					 [args]:CG.#exp*
           |	{call} [type]:CG.#type [classType]:CG.#type.class [name]:java_String [args]:CG.#exp*
           |	{notImplemented}
           |	{forIndex} [var]:java_String [from]:CG.#exp [to]:CG.#exp [by]:CG.#exp [body]:CG.#stm
           |	{forAll} [var]:java_String [exp]:CG.#exp [body]:CG.#stm
           |	{while} [exp]:CG.#exp [body]:CG.#stm
           |	{letBeSt} [header]:CG.#letBeSt.header [statement]:CG.#stm
           |	{throw} [exp]:CG.#exp
           |	{forLoop} [init]:CG.#decl.#Local.var [cond]:CG.#exp [inc]:CG.#exp [body]:CG.#stm
           |	{increment} [var]:CG.#exp
           |	{decrement} [var]:CG.#exp
           |	{raiseError} [error]:CG.#exp.#RuntimeError
           |	{cases} [exp]:CG.#exp [cases]:CG.#stm.#altStm.case* [others]:CG.#stm
           |	#altStm
           |	{error}
           |	{continue}
           |	{break}
           |	{start}
           		[exp]:CG.#exp 
           		//[type]:CG.#type
           ;
           
#altStm {-> package='org.overture.codegen.cgast.statements'}
    = {case} [pattern]:CG.#pattern [result]:CG.#stm [patternType]:CG.#type
    ;
           
#letBeSt {-> package='org.overture.codegen.cgast.utils'}
	= {header} [binding]:CG.#multipleBind.set [suchThat]:CG.#exp //The Overture AST uses a multiplebind 
	;
	
#exp {-> package='org.overture.codegen.cgast.expressions'
	| [type]:CG.#type}
    =	#Unary
    |	#Binary
    |	{field} [object]:CG.#exp [memberName]: java_String
    |	{apply} [root]:CG.#exp [args]:CG.#exp*
	|	{new} [name]:CG.#name.type [args]:CG.#exp*
	|	#Var
	|	{instanceof} [checkedType]:CG.#type [exp]:CG.#exp
    |	#Literal
    |	{self}
    |	{null}
    |	{letDef} [localDefs]:CG.#decl.#Local.var* [exp]:CG.#exp
    |	{methodInstantiation} [func]:CG.#exp [actualTypes]:CG.#type*
    |	{tuple} [args]:CG.#exp* [tupleType]:CG.#type.tuple
    |	{fieldNumber} [field]:java_Long [tuple]:CG.#exp
    |	{tupleSize} [tuple]:CG.#exp
    |	{ternaryIf} [condition]:CG.#exp [trueValue]:CG.#exp [falseValue]:CG.#exp
    |   {maplet}  [left]:CG.#exp [right]:CG.#exp
    |	{letBeSt} [header]:CG.#letBeSt.header [value]:CG.#exp
    |	#Seq
    |	#Set
    |	#Map
    |	{mkBasic} [arg]:CG.#exp
    |	#Quantifier
    |	{preInc} [exp]:CG.#exp
    |	{preDec} [exp]:CG.#exp
    |	{postInc} [exp]:CG.#exp
    |	{postDec} [exp]:CG.#exp
    |	{deRef} [exp]:CG.#exp //For pointer dereference
    |	#RuntimeError
        //When generating code using the abstract syntax of this tree structure "external" 
    	//is used for refering to expressions of the target environment (e.g. that of Java) 
    	//that are not directly mapped to by the code generator.
    |	{external} [targetLangExp]:java_String
    |	{lambda} [params]:CG.#localParam.formalParam* [exp]:CG.#exp
    |	{anonymousClass} [methods]:CG.#decl.method*
    |	{notImplemented}
    |	{undefined}
    |	{threadId}
    |	{tupleCompatibility} [tuple]:CG.#exp [types]:CG.#type*
    |	{cases} [exp]:CG.#exp [cases]:CG.#exp.#altExp.case* [others]:CG.#exp
    |	#altExp
    |	{subSeq} [seq]:CG.#exp [from]:CG.#exp [to]:CG.#exp
    ;

#altExp {-> package='org.overture.codegen.cgast.expressions'}
    = {case} [pattern]:CG.#pattern [result]:CG.#exp [patternType]:CG.#type
    ;

#RuntimeError {-> package='org.overture.codegen.cgast.expressions'}
    =   {LetBeStNoBinding}
    |	{patternMatch} [message]:java_String
    |	{missingMember} [message]:java_String
    	//Raised at runtime when an attemp is made 
		//to access a non-existing member (e.g. operation)
    ;

#Var {-> package='org.overture.codegen.cgast.expressions'
	|	[isLambda]:java_Boolean}
    =   {identifier} [original]:java_String //Change original to name...
	|	{explicit} [classType]:CG.#type.class [name]:java_String
    ;
    
#Literal {-> package='org.overture.codegen.cgast.expressions'}
    =	{int} [value]:java_Long
    |	{real} [value]:java_Double
    |	{bool} [value]:java_Boolean
    |	{char} [value]:java_Character
    |	{quote} [value]:java_String
    |	{string} [value]:java_String [isNull]:java_Boolean
	;
	
#Quantifier {-> package='org.overture.codegen.cgast.expressions'
	| [bindList]:CG.#multipleBind.set*
	| [predicate]:CG.#exp}
	= {forAll}
	| {exists}
	| {exists1}
	;
    
#Binary {-> package='org.overture.codegen.cgast.expressions'
	| [left]:CG.#exp
	| [right]:CG.#exp}
    =   #Numeric
    |	#Bool
    //Special tree construct NOT used when constructing the OO AST from 
    //the VDM model. The addr comparison constructs can, however, be convenient 
    //when generating real code (e.g., Java) that is first described using 
    //the abstract syntax of the OO AST.
    |	{addrEquals}
    |	{addrNotEquals} 
    |	{equals}
    |	{notEquals}
    |	{seqConcat}
    |	{seqModification}
    |   {inSet}
    |	{setUnion}
    |	{setIntersect}
    |	{setDifference}
    |	{setSubset}
    |	{setProperSubset}
    |	{mapUnion}
    |	{mapOverride}
    |	{domainResTo} //Syntax: '<:'
    |	{domainResBy} //Syntax: '<-:'
    |	{rangeResTo} //Syntax: ':>'
    |	{rangeResBy} //Syntax: ':->'
    ;
 
#Numeric {-> package='org.overture.codegen.cgast.expressions'}
    =	{div}
    |	{divide}
    |	{greaterEqual}
    |	{greater}
    |	{lessEqual}
    |	{less}
    |	{mod}   
    |	{plus} 
    |   {subtract}
    |	{rem}
    |	{times}
    |	{power}       
    ;   

#Bool {-> package='org.overture.codegen.cgast.expressions'}
    =	{or}
    |	{and}
    |	{xor}
	;
 
#Unary {-> package='org.overture.codegen.cgast.expressions'
	| [exp]:CG.#exp}
    =   {plus} 
    |   {minus}
    |	{cast}
    |	{isolation}
    |	{size} //Used for both sets and sequences
    |	{elems}
    |	{indices}
    |	{head}
    |	{tail}
    |	{reverse}
    |	{floor}    
    |	{abs}
    |	{not}
    |	{distConcat}
    |	{distUnion}
    |	{distIntersect}
    |	{powerSet}
    |	{mapDomain}
    |	{mapRange} 
    |	{distMerge}
    |	{mapInverse}
    ;
    
#Seq {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp*
	| {comp} [first]:CG.#exp [setBind]:CG.#bind.set [set]:CG.#exp [predicate]:CG.#exp
	;

#Set {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp*
	| {comp} [first]:CG.#exp [bindings]:CG.#multipleBind.set* [predicate]:CG.#exp//In the Overture AST bindings are of type multipleBind* as it also allows type binds.
	| {range} [first]:CG.#exp [last]:CG.#exp
	;

#Map {-> package='org.overture.codegen.cgast.expressions'}
	= {enum} [members]:CG.#exp.maplet*
	| {comp} [first]:CG.#exp.maplet [bindings]:CG.#multipleBind.set* [predicate]:CG.#exp
	;

#type {-> package='org.overture.codegen.cgast.types'}
    =   #basic
    |	{Object}
    |	{void}
    |	{class} [types]:CG.#type* [name]:java_String
    	//When generating code using the abstract syntax of this tree structure "external" 
    	//is used for refering to types of the target environment (e.g. that of Java) 
    	//that are not directly mapped to by the code generator.
    |	{external} [name]:java_String [info]:externalType.info
    |	{record} [name]:CG.#name.type
    |	{string}
    |	{template} [name]:java_String
    |	{tuple} [types]:CG.#type*
    |   {method} [params]:CG.#type* [result]:CG.#type [equivalent]:vdmType
    |	#map
    |	#set
    |	#seq
    |	#basicTypeWrappers
    |	{error}
    |	{interface} [types]:CG.#type* [name]:java_String
    |	{union} [types]:CG.#type*
    |	{quote} [value]:java_String
    |	{unknown}
    ;
    
externalType {-> package='org.overture.codegen.cgast.utils'}
	= {info} [namespace]:java_String [filePath]:java_String 
	;

#basicTypeWrappers {-> package='org.overture.codegen.cgast.types'}
	= {Int}
	| {Real}
	| {Char}
	| {Bool}
	;

#set {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [setOf]:CG.#type}
    =   {set} 
    ;
  
    
#seq {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [seqOf]:CG.#type}
    =   {seq}
    ;
    
#map {-> package='org.overture.codegen.cgast.types'
	| [empty]:java_Boolean
	| [from]:CG.#type
	| [to]:CG.#type }
    =   {map}
    ;
    
#basic {-> package='org.overture.codegen.cgast.types'}
    =   #numeric
    |	{char}
    |	{bool}
    |	{token}
    ;

#numeric {-> package='org.overture.codegen.cgast.types'}
    =   {int}
    |   {real} 
    ;
     
Aspect Declaration
/* This section allows fields of root types to be declared e.g. exp*/

//1) No check for clash wrt nameing of fields
//2) Only graphs
